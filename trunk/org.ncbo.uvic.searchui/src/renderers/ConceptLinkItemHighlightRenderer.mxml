<?xml version="1.0" encoding="utf-8"?>
<ui:URLLinkButton xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:ui="flex.utils.ui.*" implements="mx.core.IFactory" 
	textAlign="left" openInSameWindow="true">
	
	<mx:Script>
	<![CDATA[
		import org.ncbo.uvic.flex.doi.DegreeOfInterestService;
		import org.ncbo.uvic.flex.logging.LogService;
		import org.ncbo.uvic.flex.util.NavigateToBioPortal;
		import events.ConceptLinkEvent;
		import flex.utils.ui.TextHighlighter;
		import org.ncbo.uvic.flex.model.NCBOSearchResultConcept;

		private static const DOI_RESULTS:String = "searchResults";

		public var highlighter:TextHighlighter;
		private var styleNameSet:Boolean;
		
		override public function set data(value:Object):void {
			super.data = value;
			enabled = false;
			url = null;
			toolTip = null;
			
			var concept:NCBOSearchResultConcept = (value as NCBOSearchResultConcept);
			if (concept) {
				label = concept.name;
				if (concept.id) {
					enabled = true;
					toolTip = concept.name + " (click to view in BioPortal)";
					url = NavigateToBioPortal.getBioPortalURL(concept.ontologyVersionID, concept.id);
					if (getStyle("textDecoration") == "none") {
						setStyle("textDecoration", "underline");
					}
					useHandCursor = true;
				} else {
					setStyle("textDecoration", "none");
					useHandCursor = false;
				}
			}
		} 
		
		override protected function openURL(windowName:String):Boolean {
			var ok:Boolean = super.openURL(windowName);
			if (ok) {
				var concept:NCBOSearchResultConcept = (data as NCBOSearchResultConcept);
				if (concept) {
					LogService.logConceptEvent(concept, concept.ontologyID, NavigateToBioPortal.LOG_LINK);
				}
			}
			return ok;
		}
		
		public function newInstance():*	{
			var renderer:ConceptLinkItemHighlightRenderer = new ConceptLinkItemHighlightRenderer();
			renderer.highlighter = highlighter;
			return renderer;
		}
		
		override protected function updateDisplayList(w:Number, h:Number):void {
			super.updateDisplayList(w, h);
			var concept:NCBOSearchResultConcept = (data as NCBOSearchResultConcept);
			if (DegreeOfInterestService.isHighlighting(DOI_RESULTS)) {
				var style:String = DegreeOfInterestService.getStyleName(concept, DOI_RESULTS);
				if (style != styleName) {
					styleName = style;
					styleNameSet = true;
				}
			} else {
				if (styleNameSet && (styleName != null)) {
					styleName = null;
					styleNameSet = false;
				}
				if (concept && highlighter && highlighter.enabled) {
					highlighter.highlightText(textField/*, concept.searchText*/);
				}
			}
		}
	
	]]>
	</mx:Script>
	
</ui:URLLinkButton>
